


//---------------------------------------------------------------------------------------
// MovieRoot.ad
//
// (ä¾‹)
// MovieRoot.ad
//---------------------------------------------------------------------------------------


//---------------------------------------------------------------------------------------
// function
//---------------------------------------------------------------------------------------
module ROOT
{
    hidden.visible = false;

    attribute finished = false;
    attribute language_list;

    method open(context, langs)
    {
        self.language_list = ["SYSTEM"];
        self.language_list.append(langs);
        SequenceUtil::startPageDirect(context, ROOT);
    }

    method close(context)
    {
        context.closePage(ROOT);
        self.finished = true;
    }

    method isFinished()
    {
        return self.finished;
    }

    method onInitialize(context)
    {
        LangUtil::translate_text(ROOT);
        DialogFrame::Pane::Body::List::ListBox.initialize(context);
    }

    method onFinalize(context)
    {
        DialogFrame::Pane::Body::List::ListBox.setItemCount(0);
    }

    module DialogFrame::Pane::Body::List::ListBox
    {
        method initialize(context)
        {
            var tmp = hidden::Item;
            var size = ROOT.language_list.size;
            ListBox.setItemTemplate(context, tmp);
            ListBox.setItemCount(size);
            
            var total_space = ListBox.spacing * (size - 1);
            var border_width = 20 * 2;
            ListBox.minimum_height = (tmp.h * size) + total_space + border_width;
        }

        method onVisibleNotify(context, indices)
        {
            var count = self.getItemCount();
            foreach (var i in indices)
            {
                if (self.getItemVisible(i))
                {
                    var item = self.getItemWidget(i);
                    if (item == nil)
                    {
                        item = self.getItemWidget(i, context);
                    }
                    if (item.language == nil)
                    {
                        item.initialize(context, ROOT.language_list[i]);
                    }
                }
            }
        }

        method onFocusNotify(context, new_index, old_index)
        {
            var new_item = self.getItemWidget(new_index, context);
            if (new_item != nil)
            {
                ROOT.setFocus(new_item);
            }
            return 2;
        }

        method onActivate(context)
        {
            main::sound.play("ok");
            var item = self.getItemWidget(self.focus_index, context);
            var language = item.language;
            var changed = false;

            if (language != "SYSTEM")
            {
                var res = false;
                var str = "";

                switch (language)
                {
                    case "EL":
                        str = "GREEK";
                        break;
                    case "CZ":
                        str = "CZECH";
                        break;
                    case "HU":
                        str = "HUNGARIAN";
                        break;
                    case "MS":
                        str = "MEXICANSPANISH";
                        break;
                }

                var text0 = manager.translate(ROOT, "DO_YOU_REALLY_WANT_TO_SET_TO_%{str}_%{str}");
                var text1 = manager.translate(ROOT, "DO_YOU_REALLY_WANT_TO_SET_TO_%{str}");

                var res = DialogUtil::openConfirmDialog(context, DialogUtil::DEFAULT_NO, text0 + "\n\n" + text1);
                if (res)
                {
                    var option = GAME_STATUS.user_profile.option;
                    option.extra_language = pdistd::Language[language];
                    option.extra_language_done = true;
                    if (option.extra_language != pdistd::Language::SYSTEM)
                    {
                        pdistd::SetLanguage(option.extra_language);
                        changed = true;
                    }
                }
                else
                {
                    return 2;
                }
            }
            else
            {
                var option = GAME_STATUS.user_profile.option;
                if (option.extra_language != pdistd::Language::SYSTEM)
                {
                    option.extra_language = pdistd::Language::SYSTEM;
                    option.extra_language_done = false;
                    pdiext::SetStaticLanguage();
                    changed = true;
                }
            }

            if (changed)
            {
                var l = pdistd::MLocale::getLanguage();
                Grim2.setLanguage(l.downcase());
                var option = GAME_STATUS.user_profile.option;
                pdiext::MUnit::SetUnitSystemByLanguage(pdistd::MLocale::getLanguage());
                option.unit_velocity = pdiext::MUnit::GetCurrentUnitOfVelocity();
                LangUtil::apply(context);
                LangUtil::translate_text(BootProject);
            }

            ROOT.close(context);
            return 2;
        }
    }

    module hidden::Item
    {
        attribute language;
		
        method initialize(context, language)
        {
            self.language = language;
            self.label.text = manager.translate("Language", language).build(manager.translate("Language", pdistd::MLocale::getDefaultLanguage()));
        }
    }
}